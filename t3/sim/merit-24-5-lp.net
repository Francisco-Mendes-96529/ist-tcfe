*
* NGSPICE simulation script
* BJT amp with feedback
*

* forces current values to be saved
.options savecurrents



.param xC=1u
.param xR=0.1k
.param xClp=0.2u
.param xRlp=0.01k
*.param xClp=0u
*.param xRlp=0k

* voltage source
Vac T1 T0 dc 0 ac 1 sin(0 13.115 50)

* bridge rectifier
Db1 T1 1 Default
Db2 T0 1 Default
Db3 0 T0 Default
Db4 0 T1 Default

Cc 1 0 xC

* voltage regulator
Rr 1 vo xR
Dr vo 0 Nx

*low-pass
Rlp vo volp xRlp
Clp volp 0 xClp

.model Default D
.model Nx D(N=25)




.tran 2e-4 0.4 0.2
.measure tran media AVG v(vo) from=.2 to=.4
.measure tran media0 param='abs(media-12)'
.measure tran maxvo MAX v(vo) from=.2 to=.4
.measure tran minvo MIN v(vo) from=.2 to=.4
.measure tran ripple param='maxvo - minvo'

.measure tran cost param='(25+4)*0.1 + (xC+xClp)*10^6 + (xR+xRlp)*10^(-3)'
.measure tran merit param='1 / (cost * (ripple + media0 + 10^(-6)))'

.measure tran medialp AVG v(volp) from=.2 to=.4
.measure tran medialp0 param='abs(medialp-12)'
.measure tran maxvolp MAX v(volp) from=.2 to=.4
.measure tran minvolp MIN v(volp) from=.2 to=.4
.measure tran ripplelp param='maxvolp - minvolp'

.measure tran costlp param='(25+4)*0.1 + (xC+xClp)*10^6 + (xR+xRlp)*10^(-3)'
.measure tran meritlp param='1 / (costlp * (ripplelp + medialp0 + 10^(-6)))'

.control
run

*makes plots in color
set hcopypscolor=0
set color0=white
set color1=black
set color2=red
set color3=blue
set color4=violet
set color5=rgb:3/8/0
set color6=rgb:4/0/0



let vo = v(vo)

hardcopy dc-24-5-lp.ps (vo-12) vo abs(v(T1,T0)) v(volp)
echo dc-24-5-lp_FIG


quit
.endc

.end
