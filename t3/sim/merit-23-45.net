*
* NGSPICE simulation script
* BJT amp with feedback
*

* forces current values to be saved
.options savecurrents


* voltage source
Vac Vin 0 dc 0 ac 1 sin(0 230 50)

* 'transformer'
F1 Vin 0 E2 '13.115/230'
E2 va vb Vin 0 '13.115/230'

* bridge rectifier
Db1 va ve Default
Db2 vb ve Default
Db3 0 vb Default
Db4 0 va Default

Cc ve 0 1u

* voltage regulator
Rr ve vo 0.1k
Dr vo 0 Nx


.model Default D
.model Nx D(N=25)




.tran 2e-4 0.4 0.2

.control
run

*makes plots in color
set hcopypscolor=0
set color0=white
set color1=black
set color2=red
set color3=blue
set color4=violet
set color5=rgb:3/8/0
set color6=rgb:4/0/0


let vo = v(vo)
let xC = 1
let xR = 0.1

let mediave = mean(ve)
let rippleve = maximum(ve)-minimum(ve)

let mediavo = mean(vo)
let ripplevo = maximum(vo)-minimum(vo)

let media0 = abs(mediavo-12)
let cost = '(25+4)*0.1 + xC + xR'
let merit = '1 / (cost * (ripplevo + media0 + 10^(-6)))'

print media0

echo  "t3_TAB"
print mediave
print rippleve
print mediavo
print ripplevo
print merit
echo  "t3_END"


hardcopy dc-23-45.ps ve vo vo-12
echo dc-23-45_FIG


quit
.endc

.end
